#!/bin/zsh

bmc_palette_sha256 () {
    if command -v sha256sum >/dev/null 2>&1
    then
        sha256sum "$@"
    else
        shasum -a 256 "$@"
    fi
}

bmc_palette_ () {
    local bold="$1"
    local ctrue="$2"
    local c256="$3"
    local c16="$4"
    local -a args=()

    [[ $bold = yes ]] && args+=(--bold)
    [[ -n $ctrue ]] && args+=(--color-true $ctrue)
    [[ -n $c256 ]] && args+=(--color-256 $c256)
    [[ -n $c16 ]] && args+=(--color-16 $c16)

    color_fg $args
}

bmc_palette () {
    local bold
    local pattern
    local -A colors

    setopt localoptions shwordsplit

    colors[user]='      yes 0x0086ff 33  blue'
    colors[pwd]='       yes 0x4bdb4b 40  green'
    colors[unpriv]='    yes 0xffffff 15  white'
    colors[priv]='      yes 0xff0101 9   red'
    colors[date]='      yes 0x7186d8 104 magenta'
    colors[time]='      yes 0x7186d8 104 magenta'
    colors[retcode]='   yes 0xff0000 9   red'
    colors[signame]='   yes 0xff0000 9   red'
    colors[tty]='       yes 0xffffff 15  white'
    colors[privuser]='  yes 0xff0101 9   red'
    colors[unprivuser]='yes 0x0086ff 33  blue'
    colors[host]='      no  0x0086ff 33  cyan'
    colors[vcs]='       no  0x0086ff 33  cyan'
    colors[space]='     no  0xffffff 15  white'
    colors[bold]="      yes 0xff6274 9   red"
    colors[underline]=" yes 0x4bde97 40  green"
    colors[standout]="  yes 0x0086ff 33  blue"
    colors[grep]="      yes 0x0086ff 33  blue"

    if grep -qs Microsoft /proc/version 2>/dev/null
    then
        colors[hostcolor]="no 0xffffff 15 white"
    else
        local hostname=$( (hostname -f 2>/dev/null || hostname 2>/dev/null || printf localhost) | tr -d '\n')
        local hostcolor="$(printf '%s' "$hostname" | bmc_palette_sha256)"

        # In Docker, use the hostname as the color instead of a hash of it.
        grep -qs docker /proc/1/cgroup && grep -qsE '^[a-f0-9]{8}' <<<"$hostname" && hostcolor="$hostname"

        colors[hostcolor]="no
                           0x$(cut -b1-6 <<<"$hostcolor")
                           0x$(cut -b7-8 <<<"$hostcolor")
                           white"
    fi

    local key val
    for key val in "${(@kv)colors}"
    do
        bmc_palette_colors[$key]=$(bmc_palette_ $colors[$key])
    done
}

bmc_palette
